buildscript {
  ext {
    springBootVersion = '2.1.3.RELEASE'
  }
  repositories {
    mavenLocal()
    jcenter()
    maven { url "http://repo1.maven.org/maven2/" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '2.7.1'
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.83")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
  id 'jacoco'
  id 'net.ltgt.apt' version '0.10'
  id 'io.franzbecker.gradle-lombok' version '1.14'
  id 'org.owasp.dependencycheck' version '5.2.3'
  id 'org.sonarqube' version '2.7.1'
  id 'org.springframework.boot' version '2.1.3.RELEASE'
  id 'pmd'
  id 'info.solidsoft.pitest' version '1.3.0'

}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.sonarqube'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'jacoco'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'info.solidsoft.pitest'


group = 'uk.gov.hmcts.reform.finrem'
version = '0.0.1'
mainClassName = 'uk.gov.hmcts.reform.finrem.notifications.NotificationApplication'
sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
  mavenLocal()
  jcenter()
  maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
  maven { url "http://repo.maven.apache.org/maven2" }
  maven { url 'https://repo.spring.io/libs-milestone' }
}


sourceSets {
  testSmoke {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  test {
    java.srcDir 'src/test/java'
    resources.srcDir 'src/test/resources'
  }

  functional {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDirs = ['src/functionalTests/java']


    }
    resources {
      srcDirs = ['src/functionalTests/resources']
    }

  }
}

configurations {
  functionalCompile.extendsFrom testCompile
  functionalRuntime.extendsFrom testRuntime
}


def versions = [
        springfoxSwagger     : '2.9.2',
        commonsIo            : '2.5',
        wiremockVersion      : '2.8.0',
        reformsJavaLogging   : '5.0.1',
        serviceTokenGenerator: '1.0.4',
        springRetry          : '1.2.1.RELEASE',
        restAssured          : '3.0.3',
        hmctsNotify          : '3.8.0-RELEASE',
        commonsLang3         : '3.7',
        jsonAssert           : '1.2.3',
        junit                : '4.12',
        lombok               : '1.18.4',
        nimbus               : '5.1',
        pdfbox               : '2.0.8',
        mockito              : '2.8.9',
        powerMock            : '1.7.3',
        puppyCrawl           : '7.6',
        serenity             : '2.0.42',
        serenityCucumber     : '1.1.3',
        springBoot           : '2.1.3.RELEASE',
        springOpenFeign      : '2.0.0.RELEASE',
        unirest              : '1.4.9',
        wiremockVersion      : '2.18.0',
        pitest               : '1.3.2',
        gradlePitest         : '1.4.0',
        sonarPitest          : '0.5',
        commonsBeanUtils     : '1.9.4',
]

configurations.all {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails dependencyDetails ->
      if (dependencyDetails.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        dependencyDetails.useVersion '2.9.9'
      }
    }
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-configuration-processor")
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformsJavaLogging
  compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'
  compile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
  compile group: 'org.springframework.retry', name: 'spring-retry', version: versions.springRetry
  compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: versions.hmctsNotify

  compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version:'0.0.5'
  compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'

  compile (group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils) {
    force = true
  }

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: versions.wiremockVersion

  testSmokeCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testSmokeCompile sourceSets.main.runtimeClasspath
  testSmokeCompile sourceSets.test.runtimeClasspath

  functionalCompile group: 'junit', name: 'junit', version: versions.junit
  functionalCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  functionalCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  functionalCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  functionalCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  functionalCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
  functionalCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
  functionalCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  functionalCompile group: 'junit', name: 'junit', version: versions.junit
  functionalCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
  functionalCompile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
  functionalCompile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  functionalCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  functionalCompile group: 'org.pdfbox', name: 'com.springsource.org.pdfbox', version: '0.7.3'
  functionalCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
}
def sonarExclusions = ['**/uk/gov/hmcts/reform/finrem/notifications/config/**',
                       '**/uk/gov/hmcts/reform/finrem/notifications/domain/**',
                       '**/uk/gov/hmcts/reform/finrem/notifications/NotificationApplication'
]

sonarqube {
  println "echo sonarqube"
  properties {
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.projectKey", "finrem-notification-service"
    property "sonar.dynamicAnalysis", "reuseReports"
    property "sonar.java.coveragePlugin", "jacoco"
    property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
}

checkstyle.toolVersion = '7.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

pmd {
  toolVersion = "6.5.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("ruleset.xml")
}

jacocoTestReport {
  executionData(test)

  reports {
    xml {
      enabled true
    }

    html {
      enabled true
    }
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

pitest {
  targetClasses = ['uk.gov.hmcts.reform.finrem.notifications.*']
  excludedClasses = ['uk.gov.hmcts.reform.finrem.notifications.NotificationApplication',
                     'uk.gov.hmcts.reform.finrem.notifications.model.*',
                     'uk.gov.hmcts.reform.finrem.notifications.config.*']
  threads = 4
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 80
}

compileJava.dependsOn(processResources)
build.dependsOn jacocoTestReport

bootJar {
  manifest {
    attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
  }
  archiveName 'finrem-notification-service.jar'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.testSmoke.output.classesDirs
  classpath = sourceSets.testSmoke.runtimeClasspath
  finalizedBy aggregate
}

task functional(type: Test) {
  description = "Runs functional Tests"
  setTestClassesDirs(sourceSets.functional.output.classesDirs)
  setClasspath(sourceSets.functional.runtimeClasspath)
  finalizedBy aggregate
}
